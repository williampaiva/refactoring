-- @atlcompiler atl2010
-- @path Java=/org.eclipse.gmt.modisco.java/model/java.ecore


module Refactoring;
create OUT: Java refining IN: Java;

helper context Java!FieldDeclaration def: hasGetter() : Boolean = 
	self.abstractTypeDeclaration.bodyDeclarations
	->select(d | d.oclIsTypeOf(Java!MethodDeclaration))
	->select(md | md.name.startsWith(
		let fieldName : String = self.fragments->first().name 
		in
			'get' + fieldName.substring(1,1).toUpper() + fieldName.substring(2,fieldName.size())
		))
	->notEmpty();

helper context String def: firstToUpper() : String =
	self.substring(1, 1).toUpper() + self.substring(2, self.size());

rule CreateGetter{
	from
		field : Java!FieldDeclaration(
			not field.hasGetter()	
		)
	to
		-- Change member visibility - public to private
		updatedField: Java!FieldDeclaration(
			modifier <- fieldModifier
		),
		fieldModifier : Java!Modifier (
			visibility <- #private	
		),
		
		-- Generate getter
		getter : Java!MethodDeclaration (
			abstractTypeDeclaration <- field.abstractTypeDeclaration,
			originalCompilationUnit <- field.originalCompilationUnit,
			name <- 'get' + field.fragments->first().name.firstToUpper(),
			modifier <- getterModifier,
			returnType <- getterReturnType,
			body <- getterBlock
		),
		getterModifier : Java!Modifier (
			visibility <- #public	
		),
		getterReturnType : Java!TypeAccess (
			type <- if not field.type.oclIsUndefined() then
						field.type.type
					else
						OclUndefined
					endif
		),
		getterBlock : Java!Block (
			originalCompilationUnit <- field.originalCompilationUnit,
			statements <- Sequence{getterBlockReturnStatement}
		),
		getterBlockReturnStatement : Java!ReturnStatement (
			originalCompilationUnit <- field.originalCompilationUnit,
			expression <- getReturnExpression
		),
		getReturnExpression : Java!FieldAccess (
			originalCompilationUnit <- field.originalCompilationUnit,
			field <- getStringVariableAccess,
			expression <- getExpression
		),
		getStringVariableAccess : Java!SingleVariableAccess (
			variable <- field.fragments->first()	
		),
		getExpression : Java!ThisExpression (
			originalCompilationUnit <- field.originalCompilationUnit
		)
}